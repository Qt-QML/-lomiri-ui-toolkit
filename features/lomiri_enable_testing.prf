instbase = $$ROOT_BUILD_DIR/qml

!qt_submodule_build {
    # These bizarre rules copy the files to the build directory

    defineReplace(qmlModStripSrcDir) {
        return($$relative_path($$1, $$_PRO_FILE_PWD_))
    }

    lomiriqmlfiles2build.input = fq_qml_files
    lomiriqmlfiles2build.output = $$instbase/$$TARGETPATH/${QMAKE_FUNC_FILE_IN_qmlModStripSrcDir}
    !contains(TEMPLATE, vc.*): lomiriqmlfiles2build.variable_out = PRE_TARGETDEPS
    lomiriqmlfiles2build.commands = $$QMAKE_COPY ${QMAKE_FILE_IN} ${QMAKE_FILE_OUT}
    lomiriqmlfiles2build.name = COPY ${QMAKE_FILE_IN}
    lomiriqmlfiles2build.CONFIG = no_link no_clean

    QMAKE_EXTRA_COMPILERS += lomiriqmlfiles2build
}
